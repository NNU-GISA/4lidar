if(VTK_VERSION VERSION_LESS "6.0")
  message(STATUS "VTK version is 5 or below, therefore using Qt4")
  find_package(Qt4 REQUIRED)
  include(${QT_USE_FILE})
  include(${VTK_USE_FILE})
  macro(qt_wrap_ui)
    qt4_wrap_ui(${ARGN})
  endmacro()
  list(APPEND VTK_LIBRARIES QVTK)
else()
  message(STATUS "VTK version is 6 or above, therefore using Qt5")
  find_package(Qt5Widgets REQUIRED)
  macro(qt_wrap_ui)
    qt5_wrap_ui(${ARGN})
  endmacro()
  set(QT_LIBRARIES ${Qt5Widgets_LIBRARIES})
endif()

# Find includes in corresponding build directories
set(CMAKE_INCLUDE_CURRENT_DIR ON)
# Instruct CMake to run moc automatically when needed.
set(CMAKE_AUTOMOC ON)
add_definitions(${QT_DEFINITIONS} "-DQT_NO_KEYWORDS")

macro(ADD_QT_FORM _prefix _name)
  list(APPEND ${_prefix}_SOURCES ${_name}.cpp)
  list(APPEND ${_prefix}_HEADERS ${_name}.h)
  list(APPEND ${_prefix}_FORMS ${_name}.ui)
endmacro(ADD_QT_FORM)

add_subdirectory(rws)
add_subdirectory(delineation)
